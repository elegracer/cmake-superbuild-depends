if(NOT TARGET depends::yaml-cpp)
  add_library(depends::yaml-cpp INTERFACE IMPORTED GLOBAL)
  if(TARGET options::moderncpp)
    target_link_libraries(depends::yaml-cpp INTERFACE options::moderncpp)
  else()
    message(FATAL_ERROR "depends::yaml-cpp expects options::moderncpp")
  endif()
  FetchContent_Declare(
    depends-yaml-cpp
    GIT_REPOSITORY https://github.com/jbeder/yaml-cpp.git
    GIT_TAG        yaml-cpp-0.6.2
  )
  FetchContent_GetProperties(depends-yaml-cpp)
  if(NOT depends-yaml-cpp_POPULATED)
    message(STATUS "Fetching yaml-cpp sources")
    FetchContent_Populate(depends-yaml-cpp)
    message(STATUS "Fetching yaml-cpp sources - done")
  endif()
  set(YAML_CPP_BUILD_TESTS OFF CACHE BOOL "" FORCE)
  set(YAML_CPP_BUILD_TOOLS OFF CACHE BOOL "" FORCE)
  set(YAML_CPP_BUILD_CONTRIB OFF CACHE BOOL "" FORCE)
  set(YAML_CPP_INSTALL OFF CACHE BOOL "" FORCE)
  add_subdirectory(${depends-yaml-cpp_SOURCE_DIR} ${depends-yaml-cpp_BINARY_DIR})
  find_package(yaml-cpp CONFIG REQUIRED PATHS ${depends-yaml-cpp_SOURCE_DIR})
  target_include_directories(depends::yaml-cpp INTERFACE ${YAML_CPP_INCLUDE_DIR})
  target_link_libraries(depends::yaml-cpp INTERFACE ${YAML_CPP_LIBRARIES})
endif()
